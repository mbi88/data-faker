import groovy.io.FileType

plugins {
    id 'ru.vyarus.quality' version '4.4.0'
}

apply plugin: 'java-library'
apply plugin: 'maven'
apply plugin: 'jacoco'

def suitesDir = 'src/test/resources/suites/'

repositories {
    maven { url "https://repo.maven.apache.org/maven2" }
}

dependencies {
    implementation group: 'org.testng', name: 'testng', version: '7.3.0'
    implementation group: 'joda-time', name: 'joda-time', version: '2.10.9'
    implementation group: 'org.json', name: 'json', version: '20201115'
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.11'
}

test {
    useTestNG() {
        // Add test suites
        def suitesList = []
        def dir = new File(suitesDir)
        dir.eachFileRecurse(FileType.FILES) { file -> suitesList << file }
        suitesList.each { options.suites(it.path) }

        testLogging {
            events "passed", "skipped", "failed"
            exceptionFormat = 'full'
            showStandardStreams = true
        }
    }
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled true
        html.destination file("${buildDir}/reports/coverage")
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

quality {
    checkstyle = true
    pmd = true
    codenarc = true
    spotbugs = true
}

check.dependsOn jacocoTestReport
